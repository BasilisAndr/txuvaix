
Multichar_Symbols
%<n%>                     ! Noun

%<abl%>
%<dat%>
%<gen%>
%<ins%>
%<loc%>
%<nom%>
%<pl%>
%<px1pl%>
%<px1sg%>
%<px2pl%>
%<px2sg%>
%<px3sp%>
%<ter%>

%>            ! Morpheme boundary

%{А%}
%{Ӑ%}
%{Ӳ%}
%{н%}
%{м%}
%{Р%}
%{☭%}
%{´%}
%{и%}

@P.ABL.nchen@ @R.ABL.nchen@ @P.ABL.ran@ @R.ABL.ran@

LEXICON Root

Nouns ;

LEXICON NP
@P.ABL.ran@%<np%>:@P.ABL.ran@ POSS ;

LEXICON N
@P.ABL.ran@%<n%>:@P.ABL.ran@ POSS ;

LEXICON POSS
@P.ABL.nchen@%<px1pl%>:@P.ABL.nchen@%>%{Ӑ%}м%{Ӑ%}р PL ;
@P.ABL.nchen@%<px1sg%>:@P.ABL.nchen@%>%{Ӑ%}м PL ;
@P.ABL.nchen@%<px2pl%>:@P.ABL.nchen@%>%{Ӑ%}р PL ;
@P.ABL.nchen@%<px2sg%>:@P.ABL.nchen@%>%{Ӳ%} PL ;
@P.ABL.nchen@%<px3sp%>:@P.ABL.nchen@%>и PL ;
PL ;

LEXICON PL
CASES ;
@P.ABL.nchen@%<pl%>:@P.ABL.nchen@%>се%{м%} CASES ;

LEXICON CASES
%<nom%>: # ;
@R.ABL.ran@%<abl%>:@R.ABL.ran@%>%{Р%}%{А%}н # ;    ! TODO: тен в некоторых словах алсо чен
@R.ABL.nchen@%<abl%>:@R.ABL.nchen@%>%{н%}ч%{А%}н # ;
%<dat%>:%>%{н%}%{А%} # ;
%<gen%>:%>%{Ӑ%}н # ;
%<ins%>:%>п%{А%} # ;
%<loc%>:%>%{Р%}%{А%} # ;  ! алсо че
%<ter%>:%>ш%{Ӑ%}н # ;


LEXICON Nouns
Женя:Женя NP ;
Золотницкий:Золотницкий NP ;
Иванова:Иванова NP ;
Наталья:Наталья NP ;
Саша:Саша NP ;
Турку:Турку NP ;
автан:автан N ;
автобус:автобус N ;
аллея:аллея N ;
анне:анне N ;
атте:атте N ;
атте-анне:атте-анне N ;
барокко:барокко N ;
власть:власть N ;
вӑйӑ:вӑйӑ N ;
вӑкӑр:вӑкӑр N ;
герой:герой N ;
звено:звено%{´%} N ;
информаци:информаци N ;
июль:июль N ;
июнь:июнь N ;
йӗп:йӗп N ;
каҫма:каҫма N ;
кенгуру:кенгуру%{´%} N ;
кино:кино%{´%} N ;
класс:класс N ;
контролёр:контролёр N ;
кофе:кофе N ;
кролик:кролик N ;
кукӑль:кукӑль N ;
кӗнеке:кӗнеке N ;
кӗсье:кӗсье N ;
кӳлӗ:кӳлӗ N ;
лаша:лаша N ;
литература:литература N ;
май:май N ;
меню:меню N ;
металл:металл N ;
министерство:министерство N ;
музей:музей N ;
няня:няня N ;
пальто:пальто%{´%} N ;
платье:платье N ;
проспект:проспект N ;
пулӑ:пулӑ N ;
пуртӑ:пуртӑ N ;
пӳ:пӳ N ;
радио:радио N ;
сарай:сарай N ;
склад:склад N ;
стена:стена%{´%} N ;
стихия:стихия N ;
супӑнь:супӑнь N ;
сӑвӑ:сӑвӑ N ;
тенкӗ:тенкӗ N ;
тетрадь:тетрадь N ;
тимӗрҫӗ:тимӗрҫӗ N ;
ту:ту N ;
тухья:тухья N ;
уй:уй N ;
урам:урам N ;
утӑ:утӑ N ;
училище:училище N ;
хурҫӑ:хурҫӑ N ;
хӑю:хӑю N ;
хӗв:хӗв N ;
хӗвел:хӗвел N ;
чей:чей N ;
чикӗ:чикӗ N ;
чӗрӗп:чӗрӗп N ;
шоссе:шоссе%{´%} N ;
шӑши:шӑши N ;
ҫурт:ҫурт N ;
ҫын:ҫын N ;
ӗҫ:ӗҫ N ;
